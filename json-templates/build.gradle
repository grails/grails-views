group "org.grails.plugins"

ext {
    userOrg = 'grails'
    repo = 'plugins'
    isGrailsPlugin = false
    projectDesc = "JSON Views Templates"
}

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    api project(':views-json')
    compileOnly "org.grails:grails-plugin-rest:$grailsVersion"
    compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"
    compileOnly "org.grails:grails-datastore-gorm-mongodb:$mongodbVersion"
}

def compileViews = tasks.register("compileViews", JavaExec) {
    inputs.files 'src/templates'
    outputs.dir 'build/classes/groovy/main'
    mainClass.set("grails.plugin.json.view.JsonViewCompiler")
    classpath configurations.compileClasspath + configurations.runtimeClasspath
    args("src/templates", "build/classes/groovy/main", "1.11", ' ', ' ', "none", "UTF-8")
}

if (!tasks.findByName("sourcesJar")) {
    tasks.register("sourcesJar", Jar) {
        archiveClassifier.set('sources')
        archiveClassifier.convention('sources')
        from 'src/templates'
    }
}

if (!tasks.findByName("javadocJar")) {
// no javadocs, just a workaround
    tasks.register("javadocJar", Jar) {
        archiveClassifier.set('javadoc')
        archiveClassifier.convention('javadoc')
        from 'src/templates'
    }
}

classes.dependsOn(compileViews)
