plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.grails.plugins'
ext.set('projectDesc', 'JSON Views Templates')

dependencies {

    api project(':views-json')

    // Should this be api, implementation, compileOnly or provided?
    // The templates expect classes from this library as models
    // but I'm not sure in what context these templates are used
    // They were previously compileOnly
    implementation libs.grails.datastore.gorm.mongodb

    compileOnly libs.slf4j.nop // Get rid of warning about missing slf4j implementation during compileGsonViews task
}

def sourceDir = layout.projectDirectory.dir('src/templates')
def outputDir = layout.buildDirectory.dir('classes/groovy/main')
sourceSets {
    main {
        groovy {
            srcDirs = [sourceDir]
        }
    }
}

tasks.register('compileViews', JavaExec) {
    inputs.dir sourceDir
    outputs.dir outputDir
    mainClass = 'grails.plugin.json.view.JsonViewCompiler'
    classpath configurations.compileClasspath + configurations.runtimeClasspath
    args(sourceDir.asFile, outputDir.get().asFile, libs.versions.java.baseline.get(), ' ', ' ', 'none', 'UTF-8')
}

tasks.named('classes') {
    dependsOn 'compileViews'
}

java {
    sourceCompatibility = JavaVersion.toVersion(libs.versions.java.baseline.get())
    withSourcesJar()
    withJavadocJar()
}

// There are no javadocs for this project.
// This is a workaround as a javadoc jar is required for publishing.
tasks.named('javadocJar', Jar) {
    from 'src/templates'
}

apply from: rootProject.layout.projectDirectory.file('gradle/grailsCentralPublishing.gradle')