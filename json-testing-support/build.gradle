plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.grails'
ext.set('projectDesc', 'JSON Views Testing Support')


configurations.register('groovydocImplementation')

dependencies {

    api project(':views-json')
    api libs.grails.datastore.core // Used in public api

    implementation libs.grails.core
    implementation libs.grails.testing.support.core
    implementation libs.groovy.core
    implementation libs.groovy.json
    implementation libs.spock.core

    groovydocImplementation localGroovy(), {
        because 'The Groovydoc task requires the same Groovy version as Gradle'
    }
}

java {
    sourceCompatibility = JavaVersion.toVersion(libs.versions.java.baseline.get())
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Groovydoc).configureEach {
    classpath = configurations.groovydocImplementation
}

tasks.named('javadocJar', Jar) {
    from tasks.named('groovydoc')
}

apply from: rootProject.layout.projectDirectory.file('gradle/grailsCentralPublishing.gradle')