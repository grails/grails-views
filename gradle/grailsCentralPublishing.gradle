ext.set('signing.keyId', project.findProperty('signing.keyId') ?: System.getenv('SIGNING_KEY'))
ext.set('signing.password', project.findProperty('signing.password') ?: System.getenv('SIGNING_PASSPHRASE'))
ext.set('signing.secretKeyRingFile', project.findProperty('signing.secretKeyRingFile') ?: "${System.properties['user.home']}${File.separator}.gnupg${File.separator}secring.gpg")

def javaComponent = components.named('java')
project.extensions.configure(PublishingExtension) {

    it.publications.create('pluginMaven', MavenPublication) {
        artifactId project.name
        from javaComponent.get()
        versionMapping {
            usage('java-api') { fromResolutionOf('runtimeClasspath') }
            usage('java-runtime') { fromResolutionResult() }
        }
        pom {
            name = 'Grails Views'
            description = 'Provides additional view technologies to the Grails framework, including JSON and Markup views.'
        }
        pom.withXml {
            def pomNode = asNode()
            pomNode.children().last() + pomInfo

            // dependency management shouldn't be included
            try { pomNode.dependencyManagement.replaceNode({}) } catch (Throwable ignore) {}
        }
    }

    if (isSnapshot) {
        repositories {
            maven {
                credentials {
                    username = System.getenv('ARTIFACTORY_USERNAME') ?: project.findProperty('artifactoryUsername') ?: ''
                    password = System.getenv("ARTIFACTORY_PASSWORD") ?: project.findProperty('artifactoryPassword') ?: ''
                }
                url = group == 'org.grails.plugins' ?
                        uri('https://repo.grails.org/grails/plugins3-snapshots-local') :
                        uri('https://repo.grails.org/grails/libs-snapshots-local')
            }
        }
    }
}


def mavenPublication = project.extensions.findByType(PublishingExtension).publications.named('pluginMaven')
afterEvaluate {
    project.extensions.configure(SigningExtension) {
        it.required = { isReleaseVersion && gradle.taskGraph.hasTask('publish') }
        it.sign mavenPublication.get()
    }
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}

tasks.register('install') {
    dependsOn 'publishToMavenLocal'
}