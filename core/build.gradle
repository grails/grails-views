plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.grails'
ext.set('projectDesc', 'Grails Views Core')

// Groovydoc task requires the Groovy version of Gradle
configurations.register('groovydocImplementation')

dependencies {

    implementation libs.groovy.core

    implementation libs.grails.core
    implementation libs.grails.web.urlmappings
    implementation libs.grails.mimetypes
    implementation libs.grails.datastore.gorm.support
    implementation libs.grails.rest

    implementation libs.slf4j.api

    api libs.caffeine // Used in public api

    testImplementation libs.spock.core

    groovydocImplementation localGroovy(), {
        because 'The Groovydoc task requires the Groovy version of Gradle'
    }
}

java {
    sourceCompatibility = JavaVersion.toVersion(libs.versions.java.baseline.get())
    withJavadocJar()
    withSourcesJar()
}

tasks.named('javadocJar', Jar) {
    from tasks.named('groovydoc')
}

tasks.withType(Groovydoc).configureEach {
    classpath = configurations.groovydocImplementation
    groovyClasspath = configurations.groovydocImplementation
}

apply from: rootProject.layout.projectDirectory.file('gradle/grailsCentralPublishing.gradle')