plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'org.grails.grails-plugin'
    id 'signing'
}

group = 'org.grails.plugins'
ext.set('projectDesc', 'Grails Markup Views')

dependencies {

    api project(':views-core')

    implementation libs.grails.core
    implementation libs.grails.rest

    testImplementation libs.spock.core
    testRuntimeOnly libs.slf4j.nop // Get rid of warning about missing slf4j implementation during test task
}

java {
    sourceCompatibility = JavaVersion.toVersion(libs.versions.java.baseline.get())
    withJavadocJar()
    withSourcesJar()
}

tasks.named('bootJar') {
    enabled = false // Don't need a bootJar for plugin
}

tasks.named('jar', Jar) {
    enabled = true // Need a jar for plugin
    archiveClassifier = '' // Don't want the '-plain' classifier
}

tasks.named('javadocJar', Jar) {
    from tasks.named('groovydoc')
}

tasks.named('build') {
    finalizedBy 'javadocJar', 'sourcesJar'
}

apply from: rootProject.layout.projectDirectory.file('gradle/grailsCentralPublishing.gradle')