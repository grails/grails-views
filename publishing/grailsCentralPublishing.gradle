ext."signing.keyId" = rootProject.hasProperty("signing.keyId") ? rootProject.getProperty('signing.keyId') : System.getenv('SIGNING_KEY')
ext."signing.password" = rootProject.hasProperty("signing.password") ? rootProject.getProperty('signing.password') : System.getenv('SIGNING_PASSPHRASE')
ext."signing.secretKeyRingFile" = rootProject.hasProperty("signing.secretKeyRingFile") ? rootProject.getProperty('signing.secretKeyRingFile') : "${System.properties['user.home']}${File.separator}.gnupg${File.separator}secring.gpg"

ext.pomInfo = {
    delegate.url 'https://views.grails.org/latest/'
    delegate.licenses {
        delegate.license {
            delegate.name 'The Apache Software License, Version 2.0'
            delegate.url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
    delegate.scm {
        delegate.url 'https://github.com/grails/grails-views/'
        delegate.connection 'scm:git:git://github.com/grails/grails-views'
        delegate.developerConnection 'scm:git:ssh://github.com:grails/grails-views'
    }
    delegate.developers {
        delegate.developer {
            delegate.id 'graemerocher'
            delegate.name 'Graeme Rocher'
            delegate.email 'graeme.rocher@gmail.com'
        }
        developer {
            delegate.id 'puneetbehl'
            delegate.name 'Puneet Behl'
            delegate.email 'behlp@objectcomputing.com'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId project.name
            afterEvaluate {
                artifact source: sourcesJar, classifier: "sources"
                artifact source: javadocJar, classifier: "javadoc"
            }

            if (isGrailsPlugin) {
                artifact source: "${project.sourceSets.main.groovy.outputDir}/META-INF/grails-plugin.xml",
                        classifier: "plugin",
                        extension: 'xml'
            }

            pom {
                name = 'Grails Views'
                description = 'Provides additional view technologies to the Grails framework, including JSON and Markup views.'
            }

            pom.withXml {
                def pomNode = asNode()
                pomNode.children().last() + pomInfo
                try {
                    pomNode.dependencyManagement.replaceNode {}
                } catch (Throwable e) {
                    // ignore
                }

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    try {
                        it.replaceNode {}
                    } catch (Throwable e) {
                        // ignore
                    }
                }
            }
        }
    }

    if (version.endsWith('-SNAPSHOT')) {
        repositories {
            maven {
                credentials {
                    username System.getenv('ARTIFACTORY_USERNAME') ?: rootProject.properties.get('artifactoryUsername')
                    password System.getenv("ARTIFACTORY_PASSWORD") ?: rootProject.properties.get('artifactoryPassword')
                }

                if (group == 'org.grails.plugins') {
                    url "https://repo.grails.org/grails/plugins3-snapshots-local"
                } else {
                    url "https://repo.grails.org/grails/libs-snapshots-local"
                }
            }
        }
    }
}

jar {
    manifest.mainAttributes(
            "Built-By": System.properties['user.name'],
            "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
            "Implementation-Title": "Grails",
            "Implementation-Version": grailsVersion,
            "Implementation-Vendor": 'grails.org')
}

afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish")  }
        Publication[] publications = new Publication[project.publishing.publications.size()]
        project.publishing.publications.findAll().toArray(publications)
        sign(publications)
    }

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }
}



task install(dependsOn: project.tasks.withType(PublishToMavenLocal))
