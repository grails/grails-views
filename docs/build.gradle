buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-jvm:4.0.1")
    }
}

apply plugin: "org.asciidoctor.jvm.convert"

repositories {
    maven { url "https://repo.grails.org/grails/core" }
}

asciidoctor {

     baseDirFollowsSourceFile()

    resources {
        from 'src/docs/images'
        into './images'
    }

    attributes 'experimental'  : 'true',
               'compat-mode'   : 'true',
               'toc'           : 'left',
               'icons'         : 'font',
               'version'       : version,
               'sourcedir'     : "${rootProject.allprojects.find { it.name == 'views-core'}.projectDir}/src/main/groovy"
}

configurations {
    documentation.extendsFrom(compileClasspath)
    documentation {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, (Bundling) objects.named(Bundling, 'external'))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, (DocsType) objects.named(DocsType, 'javadoc'))
        }
    }
}

dependencies {
  documentation project(path: ':views-json')
  documentation project(path: ':views-markup')
  documentation project(path: ':views-gradle')
}

task apidocs(type: Groovydoc, group: 'documentation') {
    def allProjects = rootProject.allprojects.findAll { project ->
      ['views-core', 'views-json', 'views-json-testing-support', 'views-markup','views-gradle'].contains(project.name)
    }
    source allProjects.collect { project ->
      project.files('src/main/groovy')
    } 

    destinationDir = new File(buildDir, 'docs/api')
    // Might need a classpath
    docTitle = "Grails Views ${version}"
    
    classpath = configurations.documentation
    groovyClasspath = configurations.documentation
}

tasks.withType(org.gradle.api.tasks.javadoc.Groovydoc) {
    configure {
        access = GroovydocAccess.PRIVATE
        processScripts = false
        includeMainForScripts = false
        includeAuthor = true
        groovyClasspath = configurations.documentation
    }
}

task docs(type:Copy, dependsOn:[apidocs, asciidoctor], group:'documentation')  {
    from "$buildDir/asciidoc/html5"
    into "$buildDir/docs"
}
