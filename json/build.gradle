plugins {
    id 'java-library'
    id 'groovy'
    id 'org.grails.grails-plugin'
    id 'maven-publish'
    id 'signing'
}

group = 'org.grails.plugins'
ext.set('projectDesc', 'Grails JSON Views')
ext.set('isGrailsPlugin', true)

dependencies {

    api project(':views-core') // Used in public API
    api libs.grails.rest // Used in public API

    implementation libs.grails.encoder
    implementation libs.groovy.core
    implementation libs.groovy.json

    testImplementation libs.grails.testing.support.core
    testImplementation libs.grails.testing.support.gorm
    testImplementation libs.grails.datastore.gorm.hibernate5
    testImplementation libs.spock.core

    testRuntimeOnly libs.slf4j.nop // Get rid of warning about missing slf4j implementation during test task
}

def testArtifacts = ['*.gson', 'circular/**']

tasks.named('jar', Jar) {
    exclude testArtifacts
}

tasks.named('sourcesJar', Jar) {
    exclude testArtifacts
}

tasks.named('build') {
    finalizedBy 'javadocJar', 'sourcesJar'
}

apply from: rootProject.layout.projectDirectory.file('gradle/grails-plugin-config.gradle')
apply from: rootProject.layout.projectDirectory.file('gradle/java-config.gradle')
apply from: rootProject.layout.projectDirectory.file('gradle/api-docs-config.gradle')
apply from: rootProject.layout.projectDirectory.file('gradle/publishing.gradle')