plugins {
    id 'java-library'
    id 'groovy'
    id 'org.grails.grails-plugin'
    id 'maven-publish'
    id 'signing'
}

group = 'org.grails.plugins'
ext.set('projectDesc', 'Grails JSON Views')

dependencies {

    api project(':views-core')

    implementation libs.grails.core
    implementation libs.grails.rest
    implementation libs.groovy.json

    testImplementation libs.grails.testing.support.core
    testImplementation libs.grails.testing.support.gorm
    testImplementation libs.grails.datastore.gorm.hibernate5
    testImplementation libs.spock.core

    testRuntimeOnly libs.slf4j.nop // Get rid of warning about missing slf4j implementation during test task
}

java {
    sourceCompatibility = JavaVersion.toVersion(libs.versions.java.baseline.get())
    withJavadocJar()
    withSourcesJar()
}

springBoot {
    // Project has 2 classes with main methods, so we need to specify which one to select
    mainClass = 'grails.plugin.json.view.Application'
}

tasks.named('bootJar') {
    enabled = false // Don't need a bootJar for plugin
}

def testArtifacts = ['*.gson', 'circular/**']

tasks.named('jar', Jar) {
    enabled = true // Need a jar for plugin
    archiveClassifier = '' // Don't want the '-plain' classifier
    exclude testArtifacts
}

tasks.named('sourcesJar', Jar) {
    exclude testArtifacts
}

tasks.named('javadocJar', Jar) {
    from tasks.named('groovydoc')
}

tasks.named('build') {
    finalizedBy 'javadocJar', 'sourcesJar'
}

apply from: rootProject.layout.projectDirectory.file('gradle/grailsCentralPublishing.gradle')