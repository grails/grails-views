plugins {
    id "com.gradle.enterprise" version "3.10.1"
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.7'
}

gradleEnterprise {
    server = 'https://ge.grails.org'
    buildScan {
        publishAlways()
        publishIfAuthenticated()
        uploadInBackground = System.getenv("CI") == null
        capture {
            taskInputFiles = true
        }
    }
}

buildCache {
    local { enabled = System.getenv('CI') != 'true' }
    remote(HttpBuildCache) {
        push = System.getenv('CI') == 'true'
        enabled = true
        url = 'https://ge.grails.org/cache/'
        credentials {
            username = System.getenv('GRADLE_ENTERPRISE_BUILD_CACHE_NODE_USER')
            password = System.getenv('GRADLE_ENTERPRISE_BUILD_CACHE_NODE_KEY')
        }
    }}


['core',
 'json',
 'markup',
 'gradle',
 'docs',
 'json-templates',
 'json-testing-support'].each {
    include("views-$it")
    project(":views-$it").projectDir = new File(settingsDir, it)
}

include "examples-functional-tests"
project(":examples-functional-tests").projectDir = new File(settingsDir, "examples/functional-tests")

include "examples-functional-tests-plugin"
project(":examples-functional-tests-plugin").projectDir = new File(settingsDir, "examples/functional-tests-plugin")
